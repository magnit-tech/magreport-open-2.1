@precedence{
  prefix,
  pow @right,
  times @left,
  plus @left
}
@top Formula { expression }

@skip { space | LineComment }

expression {
  String |
  Number |
  parenthesizedExpression |
  binaryExpression |
  unaryExpression |
  originalFieldNameExpression |
  derivedFieldNameExpression |
  FunctionCallExpression
}

originalFieldNameExpression {"[" OriginalFieldName "]"}
OriginalFieldName {fieldName}

derivedFieldNameExpression {"[[" DerivedFieldName "]]"}
DerivedFieldName{fieldName}

parenthesizedExpression { "(" expression ")" }

binaryExpression {
  Product {expression !times "*" expression} |
  Fraction {expression !times "/" expression} |
  Sum {expression !plus "+" expression} |
  Subtraction {expression !plus "-" expression}
}

unaryExpression {
  UnaryMinus {!prefix "-" expression}
}

FunctionCallExpression {
  FunctionName "(" commaSep<expression>  ")"
}

FunctionName {identifier}

@tokens {

  identifierChar { @asciiLetter | "_"}
  identifier {identifierChar (identifierChar | @digit)*}

  fieldName {![ \t\r#;\[\]\n]+![#;\[\]\n\r\t]*}

  String { '"' (!["\\] | "\\" _)* '"' }

  Number {
    (@digit ("_" | @digit)* ("." ("_" | @digit)*)? | "." @digit ("_" | @digit)*)
      (("e" | "E") ("+" | "-")? ("_" | @digit)+)?
  }

  LineComment { "#" ![\n]* }

  space { $[ \t\n\r]+ }

}

commaSep<content> {
  "" | content ("," content?)*
}

@detectDelim
